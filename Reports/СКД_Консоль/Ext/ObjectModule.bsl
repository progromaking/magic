
Функция Сохранить(МакетСКД, пКомпоновщикНастроек = Неопределено, ВнешниеНаборыДанных = Неопределено) Экспорт
	Каталог = "C:\";
	
	СериализоватьОбъектВФайл(МакетСКД, Каталог + "СКД_Схема.xml");
	
	Если НЕ пКомпоновщикНастроек = Неопределено Тогда
		Настройки = пКомпоновщикНастроек.ПолучитьНастройки();
		СериализоватьОбъектВФайл(пКомпоновщикНастроек.ПолучитьНастройки(), Каталог + "СКД_Настройки.xml");
	КонецЕсли;
	
	Если НЕ ВнешниеНаборыДанных = Неопределено Тогда
		СериализоватьОбъектВФайл(ВнешниеНаборыДанных, Каталог + "СКД_НаборыДанных.xml");
	КонецЕсли;
	
	Дерево = Новый ДеревоЗначений;
	Дерево.Колонки.Добавить("ИмяОтчета");
	Дерево.Колонки.Добавить("СхемаКомпоновкиДанных");
	Дерево.Колонки.Добавить("Настройки");
	Дерево.Колонки.Добавить("НастройкаДляЗагрузки");
	Дерево.Колонки.Добавить("СохранятьНастройкиАвтоматически");
	Дерево.Колонки.Добавить("ВнешниеНаборыДанных");
	
	СтрокаДерева = Дерево.Строки.Добавить();
	СтрокаДерева.ИмяОтчета = "Отчет";
	СтрокаДерева.СхемаКомпоновкиДанных = МакетСКД;
	СтрокаДерева.ВнешниеНаборыДанных = ВнешниеНаборыДанных;
	
	Если НЕ Настройки = Неопределено Тогда
		ТаблицаНастройки = Новый ТаблицаЗначений;
		ТаблицаНастройки.Колонки.Добавить("Имя");
		ТаблицаНастройки.Колонки.Добавить("Настройки");
		СтрокаНастроек = ТаблицаНастройки.Добавить();
		СтрокаНастроек.Имя = "Настройка1";
		СтрокаНастроек.Настройки = Новый ХранилищеЗначения(Настройки);
		
		СтрокаДерева.Настройки = ТаблицаНастройки;
		СтрокаДерева.НастройкаДляЗагрузки = СтрокаНастроек.Имя;
	КонецЕсли;
	             
	ЗначениеВФайл(Каталог + "СКД_Отчет.dcf", Дерево);
	
	Возврат ЭтотОбъект;
КонецФункции

Функция Консоль() Экспорт
	ПолучитьФорму("ФормаО").ОткрытьМодально();
	Возврат ЭтотОбъект;
КонецФункции

Процедура СериализоватьОбъектВФайл(СериализуемыйОбъект, ИмяФайла)
	
	Сериализатор = Новый СериализаторXDTO(ФабрикаXDTO);
	ОбъектXDTO = Сериализатор.ЗаписатьXDTO(СериализуемыйОбъект);	
	
	ЗаписьXML = Новый ЗаписьXML; 
	ЗаписьXML.ОткрытьФайл(ИмяФайла, "UTF-8");
	//ЗаписьXML.УстановитьСтроку("UTF-8"); 
	ЗаписьXML.ЗаписатьОбъявлениеXML();
	ФабрикаXDTO.ЗаписатьXML(ЗаписьXML, ОбъектXDTO,,,,НазначениеТипаXML.Явное); 
	
	ЗаписьXML.Закрыть();
	
КонецПроцедуры
